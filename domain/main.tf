locals {
  service_name = (var.name == "" ? "${data.cloudfoundry_app.app.name}-${var.domain_name}" : var.name)
}

data "cloudfoundry_space" "space" {
  org_name = var.cf_org_name
  name     = var.cf_space_name
}

data "cloudfoundry_app" "app" {
  name_or_id = var.app_name_or_id
  space      = data.cloudfoundry_space.space.id
}

###########################################################################
# Domain must be manually created by an OrgManager before terraform is run:
#
# cf create-domain <%= cloud_gov_organization %> TKTK-production-domain-name
###########################################################################
data "cloudfoundry_domain" "origin_url" {
  name = var.domain_name
}

resource "cloudfoundry_route" "origin_route" {
  domain = data.cloudfoundry_domain.origin_url.id
  space  = data.cloudfoundry_space.space.id
  target {
    app = data.cloudfoundry_app.app.id
  }
}

data "cloudfoundry_service" "external_domain" {
  name = "external-domain"
}

resource "cloudfoundry_service_instance" "external_domain_instance" {
  name             = local.service_name
  space            = data.cloudfoundry_space.space.id
  service_plan     = data.cloudfoundry_service.external_domain.service_plans[var.cdn_plan_name]
  recursive_delete = var.recursive_delete
  json_params      = "{\"domains\": \"${var.domain_name}\"}"
}
